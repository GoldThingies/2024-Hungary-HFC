/PROG  A_Temperature_IG08624RH
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "Temperature IG08624RH";
PROG_SIZE	= 4291;
CREATE		= DATE 23-07-29  TIME 13:44:28;
MODIFIED	= DATE 23-08-29  TIME 08:36:16;
FILE_NAME	= TEMPERAT;
VERSION		= 0;
LINE_COUNT	= 195;
MEMORY_SIZE	= 4815;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !-- 2677 DANA ;
   2:  !-- 06 26 24 ;
   3:  !-- TEMPERATURE ;
   4:  !-- IG08624RH ;
   5:  !----------------------------- ;
   6:   ;
   7:  LBL[1] ;
   8:  !-- CHANGE TOOL AND USER FRAME ;
   9:  UTOOL_NUM=4 ;
  10:  UFRAME_NUM=1 ;
  11:J PR[140:NOK Safe] 100% CNT50    ;
  12:   ;
  13:  !-- PLACE 1, PICK NONE ;
  14:  !----------------------------- ;
  15:  IF (DI[214:OFF:Temp Part 1 Present]=OFF AND DI[215:ON :Temp Part 2 Present]=OFF AND DI[72:ON :G1 Part Present]=ON) THEN ;
  16:  !-- CHANGE TOOL AND USER FRAME ;
  17:  UTOOL_NUM=4 ;
  18:  UFRAME_NUM=1 ;
  19:J PR[60] 100% CNT50    ;
  20:  !-- APPROACH TEMP 1 ;
  21:  UTOOL_NUM=1 ;
  22:  UFRAME_NUM=1 ;
  23:J PR[R[106]] 100% CNT10 Tool_Offset,PR[13:-75mm Z Offset]    ;
  24:  COL GUARD ADJUST R[39] ;
  25:  !-- PLACE PART ;
  26:L PR[R[106]] 500mm/sec FINE    ;
  27:  !-- OPEN GRIPPER ;
  28:  CALL GRIP_1_OPEN    ;
  29:  DO[428:OFF:G1 BCR2 OK]=OFF ;
  30:  DO[221:OFF:Part Placed Temp 1]=PULSE,0.5sec ;
  31:  !-- LEAVE POSITION ;
  32:L PR[R[106]] 500mm/sec CNT10 Tool_Offset,PR[13:-75mm Z Offset]    ;
  33:    ;
  34:J PR[60] 100% CNT50   ;
  35:  //J PR[110:TempNest SafePos] 100% CNT50    ;
  36:  JMP LBL[90] ;
  37:  ENDIF ;
  38:   ;
  39:  !-- PLACE 1, PICK 2 ;
  40:  !----------------------------- ;
  41:  LBL[10] ;
  42:  IF (DI[214:OFF:Temp Part 1 Present]=OFF AND DI[72:ON :G1 Part Present]=ON) THEN ;
  43:  !-- CHANGE TOOL AND USER FRAME ;
  44:  UTOOL_NUM=4 ;
  45:  UFRAME_NUM=1 ;
  46:J PR[60] 100% CNT50    ;
  47:  !-- APPROACH TEMP 1 ;
  48:  UTOOL_NUM=1 ;
  49:  UFRAME_NUM=1 ;
  50:J PR[R[106] ] 100% CNT10 Tool_Offset,PR[13:-75mm Z Offset]    ;
  51:  COL GUARD ADJUST R[39] ;
  52:  !-- PLACE PART ;
  53:L PR[R[106]] 2000mm/sec FINE    ;
  54:  !-- OPEN GRIPPER ;
  55:  CALL GRIP_1_OPEN    ;
  56:  DO[428:OFF:G1 BCR2 OK]=OFF ;
  57:  DO[221:OFF:Part Placed Temp 1]=PULSE,0.5sec ;
  58:  !-- LEAVE POSITION ;
  59:L PR[R[106]] 4000mm/sec CNT10 Tool_Offset,PR[13:-75mm Z Offset]    ;
  60:   ;
  61:  IF (DI[206:OFF:Reference Part Reques]=ON) THEN ;
  62:  !-- DEPART 1 ;
  63:  UTOOL_NUM=4 ;
  64:  UFRAME_NUM=1 ;
  65:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
  66:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
  67:  //CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
  68:  //J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
  69:  //J PR[140:NOK Safe] 100% CNT50    ;
  70:  !-- GRSL REF PART ;
  71:  CALL REF_P4611    ;
  72:  !-- APPROACH 2 ;
  73:  UTOOL_NUM=4 ;
  74:  UFRAME_NUM=1 ;
  75:J PR[140:NOK Safe] 100% CNT50    ;
  76:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
  77:J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
  78:  UTOOL_NUM=1 ;
  79:  UFRAME_NUM=1 ;
  80:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
  81:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
  82:  ENDIF ;
  83:   ;
  84:  IF (DI[215:ON :Temp Part 2 Present]=ON AND DI[72:ON :G1 Part Present]=OFF) THEN ;
  85:  !-- APPROACH TEMP 2 ;
  86:  UTOOL_NUM=1 ;
  87:  UFRAME_NUM=1 ;
  88:L PR[118:TN2 P4611 PP G1] 2000mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
  89:  COL GUARD ADJUST R[39] ;
  90:  !-- PICK PART ;
  91:L PR[118:TN2 P4611 PP G1] 2000mm/sec FINE    ;
  92:  !-- CLOSE GRIPPER ;
  93:  CALL GRIP_1_CLOSE    ;
  94:  !-- LEAVE POSITION ;
  95:L PR[118:TN2 P4611 PP G1] 4000mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
  96:  IF (DI[72:ON :G1 Part Present]=ON) THEN ;
  97:  DO[426:OFF:G1 from Temp Nest]=ON ;
  98:  DO[224:OFF:Part Picked Temp 2]=PULSE,0.5sec ;
  99:  ENDIF ;
 100:  COL GUARD ADJUST R[38] ;
 101:   ;
 102:  UTOOL_NUM=4 ;
 103:  UFRAME_NUM=1 ;
 104:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 105:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 106:  JMP LBL[90] ;
 107:  ENDIF ;
 108:  ENDIF ;
 109:   ;
 110:  !-- PLACE 2, PICK 1 ;
 111:  !----------------------------- ;
 112:  LBL[50] ;
 113:  IF (DI[215:ON :Temp Part 2 Present]=OFF AND DI[72:ON :G1 Part Present]=ON) THEN ;
 114:  !-- CHANGE TOOL AND USER FRAME ;
 115:  UTOOL_NUM=4 ;
 116:  UFRAME_NUM=1 ;
 117:   ;
 118:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 119:J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 120:   ;
 121:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 122:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 123:  !-- APPROACH TEMP 2 ;
 124:  UTOOL_NUM=1 ;
 125:  UFRAME_NUM=1 ;
 126:J PR[118:TN2 P4611 PP G1] 100% CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
 127:  COL GUARD ADJUST R[39] ;
 128:  !-- PLACE PART ;
 129:L PR[118:TN2 P4611 PP G1] 500mm/sec FINE    ;
 130:  !-- OPEN GRIPPER ;
 131:  CALL GRIP_1_OPEN    ;
 132:  DO[422:OFF:G1 from Wash]=OFF ;
 133:  DO[222:OFF:Part Placed Temp 2 ]=PULSE,0.5sec ;
 134:  !-- LEAVE POSITION ;
 135:L PR[118:TN2 P4611 PP G1] 4000mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
 136:   ;
 137:  LBL[55] ;
 138:  !--GRSL Ref Request ;
 139:  IF (DI[206:OFF:Reference Part Reques]=ON) THEN ;
 140:  !-- DEPART 2 ;
 141:  UTOOL_NUM=4 ;
 142:  UFRAME_NUM=1 ;
 143:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 144:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 145:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 146:J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 147:J PR[140:NOK Safe] 100% CNT50    ;
 148:  !-- GRSL REF PART ;
 149:  CALL REF_P4611    ;
 150:  !-- APPROACH 2 ;
 151:  UTOOL_NUM=4 ;
 152:  UFRAME_NUM=1 ;
 153:J PR[140:NOK Safe] 100% CNT50    ;
 154:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 155:J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 156:  UTOOL_NUM=1 ;
 157:  UFRAME_NUM=1 ;
 158:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 159:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 160:  ENDIF ;
 161:  IF (DI[214:OFF:Temp Part 1 Present]=ON AND DI[72:ON :G1 Part Present]=OFF) THEN ;
 162:  !-- APPROACH TEMP 1 ;
 163:L PR[114:TN1 P4611 PP G1 ] 2000mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
 164:  COL GUARD ADJUST R[39] ;
 165:  !-- PICK PART ;
 166:L PR[114:TN1 P4611 PP G1 ] 2000mm/sec FINE    ;
 167:  !-- CLOSE GRIPPER ;
 168:  CALL GRIP_1_CLOSE    ;
 169:  !-- LEAVE POSITION ;
 170:L PR[114:TN1 P4611 PP G1 ] 4000mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
 171:  IF (DI[72:ON :G1 Part Present]=ON) THEN ;
 172:  DO[426:OFF:G1 from Temp Nest]=ON ;
 173:  DO[223:OFF:Part Picked Temp 1]=PULSE,0.5sec ;
 174:  ENDIF ;
 175:  COL GUARD ADJUST R[38] ;
 176:   ;
 177:  !******************************** ;
 178:  UTOOL_NUM=4 ;
 179:  UFRAME_NUM=1 ;
 180:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 181:J PR[110:TempNest SafePos] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 182:  ENDIF ;
 183:  ENDIF ;
 184:   ;
 185:  LBL[90] ;
 186:  !-- CHANGE TOOL AND USER FRAME ;
 187:  UTOOL_NUM=4 ;
 188:  UFRAME_NUM=1 ;
 189:   ;
 190:  CALL SET_OFFS(26,0,0,0,0,0,(-180),0) ;
 191:J PR[140:NOK Safe] 100% CNT50 Offset,PR[26:Joint Offset]    ;
 192:J PR[140:NOK Safe] 100% CNT50    ;
 193:   ;
 194:  LBL[100] ;
 195:  END ;
/POS
/END
