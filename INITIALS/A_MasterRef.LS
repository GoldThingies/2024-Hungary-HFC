/PROG  A_MasterRef
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "MasterRef";
PROG_SIZE	= 2799;
CREATE		= DATE 23-07-29  TIME 14:10:22;
MODIFIED	= DATE 23-10-20  TIME 14:36:02;
FILE_NAME	= REF_P461;
VERSION		= 0;
LINE_COUNT	= 138;
MEMORY_SIZE	= 3287;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !-- 2677 DANA ;
   2:  !-- 7 16 24 ;
   3:  !-- GRSL Reference Part Sequence ;
   4:  !-- ********* ;
   5:  !----------------------------- ;
   6:   ;
   7:  IF (DI[205:ON :Reference Part Presen]=ON AND DI[206:OFF:Reference Part Reques]=ON AND DI[72:ON :G1 Part Present]=OFF AND DI[80:OFF:G2 Part Present]=OFF) THEN ;
   8:   ;
   9:  !-- CHANGE TOOL AND USER FRAME ;
  10:  UTOOL_NUM=9 ;
  11:  UFRAME_NUM=1 ;
  13:   ;
  14:J PR[3] 100% CNT50    ;
  15:  !-- PICK FROM REF NEST ;
  16:  !----------------------------- ;
  17:  !-- OPEN GRIPPER ;
  18:  CALL GRIP_1_OPEN    ;
  19:  !-- CHANGE TOOL AND USER FRAME ;
  20:  UTOOL_NUM=4 ;
  21:  UFRAME_NUM=1 ;
  22:J PR[350] 100% CNT50    ;
  23:   ;
  24:  UTOOL_NUM=1 ;
  25:  UFRAME_NUM=1 ;
  26:L PR[134:Ref Nest P4611] 500mm/sec FINE Tool_Offset,PR[12:-100mm z Offse]    ;
  27:  !-- ADJUST COLLISION DETECT ;
  28:  COL GUARD ADJUST R[39] ;
  29:L PR[134:Ref Nest P4611] 500mm/sec FINE    ;
  30:  !-- CLOSE GRIPPER ;
  31:  CALL GRIP_1_CLOSE    ;
  32:   ;
  33:L PR[134:Ref Nest P4611] 500mm/sec CNT10 Tool_Offset,PR[12:-100mm z Offse]    ;
  34:  IF (DI[72:ON :G1 Part Present]=ON) THEN ;
  35:  DO[432:OFF:G1 GRSL Ref Part]=ON ;
  36:  DO[206:OFF:Reference Part PK Nes]=PULSE,0.5sec ;
  37:  ENDIF ;
  38:  !-- ADJUST COLLISION DETECT ;
  39:  COL GUARD ADJUST R[38] ;
  40:  UTOOL_NUM=4 ;
  41:  UFRAME_NUM=1 ;
  42:J PR[100:Flip Safe Pos] 100% CNT50    ;
  43:   ;
  44:J PR[130:Ref Nest Safe] 100% CNT50    ;
  45:   ;
  46:  UTOOL_NUM=4 ;
  47:  UFRAME_NUM=1 ;
  48:J PR[100:Flip Safe Pos] 100% CNT50    ;
  49:  IF (DI[72:ON :G1 Part Present]=OFF) THEN ;
  50:  DO[395:OFF:Ref Part Not Pkd Erro]=ON ;
  51:  WAIT DI[41:OFF:AUTO CYCLE]=OFF    ;
  52:  DO[395:OFF:Ref Part Not Pkd Erro]=OFF ;
  53:  ENDIF ;
  54:   ;
  55:  !-- EXCHANGE GRSL ;
  56:  !----------------------------- ;
  57:  CALL M_GRSL_P4611    ;
  58:  ENDIF ;
  59:  !-- GRSL REF CYCLE ;
  60:  WAIT (DI[179:OFF:GRSL Part 1 Req for U]=ON AND DI[178:OFF:GRSL MT Clear])    ;
  61:   ;
  62:  !-- PICK REF GRSL ;
  63:  !----------------------------- ;
  64:  !-- OPEN GRIPPER ;
  65:  CALL GRIP_1_OPEN    ;
  66:  !-- RESET SAFE OUTSIDE GRSL ;
  67:  DO[178:ON :Robot Clear GRSL]=OFF ;
  68:  WAIT (DI[178:OFF:GRSL MT Clear])    ;
  69:  !-- CHANGE TOOL AND USER FRAME ;
  70:  UTOOL_NUM=4 ;
  71:  UFRAME_NUM=1 ;
  72:L PR[89:GRSL SAFE G1] 2000mm/sec CNT50    ;
  73:   ;
  74:  UTOOL_NUM=1 ;
  75:L PR[88:GRSL P4611 PL G1] 500mm/sec CNT10 Tool_Offset,PR[10:-75mm Z Offset]    ;
  76:   ;
  77:  !-- ADJUST COLLISION DETECT ;
  78:  COL GUARD ADJUST R[39] ;
  79:L PR[88:GRSL P4611 PL G1] 250mm/sec FINE    ;
  80:  !-- CLOSE GRIPPER ;
  81:  CALL GRIP_1_CLOSE    ;
  82:   ;
  83:L PR[88:GRSL P4611 PL G1] 2000mm/sec CNT10 Tool_Offset,PR[22:-143.65mm Z Of]    ;
  84:  !-- IF PART PRESENT THEN ;
  85:  IF (DI[72:ON :G1 Part Present] AND DI[207:OFF:Reference Part Presen]=ON) THEN ;
  86:  DO[208:OFF:Part Picked GRSL]=PULSE,0.5sec ;
  87:  DO[432:OFF:G1 GRSL Ref Part]=ON ;
  88:  !-- CHANGE TOOL AND USER FRAME ;
  89:  UTOOL_NUM=4 ;
  90:  UFRAME_NUM=1 ;
  91:L PR[89:GRSL SAFE G1] 2000mm/sec CNT50    ;
  92:  !-- ADJUST COLLISION DETECT ;
  93:  COL GUARD ADJUST R[39] ;
  94:  IF (DI[72:ON :G1 Part Present]=ON) THEN ;
  95:  DO[208:OFF:Part Picked GRSL]=PULSE,0.5sec ;
  96:  //WAIT (DI[207:OFF:Reference Part Presen]=OFF)    ;
  97:  DO[179:OFF:Part 1 Unloaded GRSL]=ON ;
  98:  WAIT (DI[179:OFF:GRSL Part 1 Req for U]=OFF)    ;
  99:  DO[179:OFF:Part 1 Unloaded GRSL]=OFF ;
 100:  ENDIF ;
 101:  !-- CHANGE TOOL AND USER FRAME ;
 102:  UTOOL_NUM=4 ;
 103:  UFRAME_NUM=1 ;
 104:  !-- MOVE TO SAFE ;
 105:J PR[6:Safe GRSL Side] 100% CNT50    ;
 106:   ;
 107:  DO[178:ON :Robot Clear GRSL]=ON ;
 108:J PR[140:NOK Safe] 100% CNT50    ;
 109:  !-- PLACE TO REF NEST ;
 110:  !----------------------------- ;
 111:  UTOOL_NUM=4 ;
 112:  UFRAME_NUM=1 ;
 113:J PR[130:Ref Nest Safe] 100% CNT50    ;
 114:   ;
 115:  UTOOL_NUM=1 ;
 116:  UFRAME_NUM=1 ;
 117:J PR[134:Ref Nest P4611] 100% CNT10 Tool_Offset,PR[12:-100mm z Offse]    ;
 118:  !-- ADJUST COLLISION DETECT ;
 119:  COL GUARD ADJUST R[39] ;
 120:L PR[134:Ref Nest P4611] 500mm/sec FINE    ;
 121:   ;
 122:  !-- OPEN GRIPPER ;
 123:  CALL GRIP_1_OPEN    ;
 124:  DO[432:OFF:G1 GRSL Ref Part]=OFF ;
 125:  DO[205:OFF:Reference Part PL Nes]=PULSE,0.5sec ;
 126:L PR[134:Ref Nest P4611] 500mm/sec CNT10 Tool_Offset,PR[12:-100mm z Offse]    ;
 127:  !-- ADJUST COLLISION DETECT ;
 128:  COL GUARD ADJUST R[38] ;
 129:   ;
 130:  UTOOL_NUM=4 ;
 131:  UFRAME_NUM=1 ;
 132:J PR[350] 100% CNT50    ;
 133:J PR[3] 100% CNT50    ;
 134:  ENDIF ;
 135:   ;
 136:  LBL[1000] ;
 137:   ;
 138:  END ;
/POS
/END
