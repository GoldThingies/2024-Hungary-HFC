/PROG  A_260GX
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "A_260GX";
PROG_SIZE	= 2809;
CREATE		= DATE 23-06-19  TIME 15:08:48;
MODIFIED	= DATE 23-08-01  TIME 14:35:30;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 127;
MEMORY_SIZE	= 3221;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !-- 2677 DANA ;
   2:  !-- 04 30 24 ;
   3:  !-- Exchange 260GX ;
   4:  !-- ******** ;
   5:  !----------------------------- ;
   6:  !-- CHANGE TOOL AND USER FRAME ;
   7:  UTOOL_NUM=9 ;
   8:  UFRAME_NUM=1 ;
   9:J PR[3:Safe Tray Side] 100% CNT50    ;
  10:J PR[4:Safe 260 Side] 100% CNT50    ;
  11:  DO[114:ON :Robot Clear 260]=ON ;
  12:  WAIT (DI[114:OFF:260GX MT Clear])    ;
  13:   ;
  14:  WAIT (DI[115:OFF:260GX Part 1 Req for ] OR DI[116:OFF:260GX Part 1 Req for ] OR DI[42:OFF:CYCLE STOP])    ;
  15:  IF (DI[42:OFF:CYCLE STOP]),JMP LBL[1000] ;
  16:   ;
  17:  !-- PICK FROM 260 ;
  18:  !----------------------------- ;
  19:  IF (DI[115:OFF:260GX Part 1 Req for ]=ON AND DI[80:OFF:G2 Part Present]=OFF OR DI[116:OFF:260GX Part 1 Req for ]=ON AND DI[72:ON :G1 Part Present]=ON) THEN ;
  20:  !-- OPEN GRIPPER ;
  21:  CALL GRIP_2_OPEN    ;
  22:  !-- RESET SAFE OUTSIDE 260 ;
  23:  DO[114:ON :Robot Clear 260]=OFF ;
  24:  WAIT (DI[114:OFF:260GX MT Clear])    ;
  25:  !-- CHANGE TOOL AND USER FRAME ;
  26:  UTOOL_NUM=9 ;
  27:  UFRAME_NUM=1 ;
  28:J PR[31] 100% CNT50    ;
  29:   ;
  30:  UTOOL_NUM=2 ;
  31:  CALL SET_OFFS(25,0,(-600),0,0,0,0,0) ;
  32:L PR[R[100]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]] Offset,PR[25:Cartesian Offset]    ;
  33:L PR[R[100]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]]    ;
  34:   ;
  35:  !-- ADJUST COLLISION DETECT ;
  36:  COL GUARD ADJUST R[39] ;
  37:L PR[R[100]] 500mm/sec FINE    ;
  38:  !-- CLOSE GRIPPER ;
  39:  CALL GRIP_2_CLOSE    ;
  40:  WAIT    .50(sec) ;
  41:   ;
  42:L PR[R[100]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]]    ;
  43:   ;
  44:  CALL SET_OFFS(25,0,(-600),0,0,0,0,0) ;
  45:L PR[R[100]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]] Offset,PR[25:Cartesian Offset]    ;
  46:   ;
  47:J PR[31] 100% CNT25    ;
  48:   ;
  49:  !-- ROBOT CLEAR OF 260GX ;
  50:  DO[114:ON :Robot Clear 260]=ON ;
  51:  WAIT (DI[114:OFF:260GX MT Clear])    ;
  52:  !-- GRIP CHECK - Open if no part ;
  53:  IF (DI[72:ON :G1 Part Present]=OFF) THEN ;
  54:  CALL GRIP_1_OPEN    ;
  55:  ENDIF ;
  56:  !-- IF PART PRESENT THEN ;
  57:  IF (DI[80:OFF:G2 Part Present] AND DI[120:OFF:260GX Part 1 NOK]=OFF),DO[439:OFF:G2 from 260 OK]=ON ;
  58:  !-- IF NOK THEN ;
  59:  IF (DI[80:OFF:G2 Part Present] AND DI[120:OFF:260GX Part 1 NOK]),DO[440:OFF:G2 from 260 NOK]=ON ;
  60:  !-- IF PURGE COMPLETE THEN ;
  61:  IF (DI[43:OFF:PURGE REQUEST] AND DO[417:OFF:G1 Raw from Tray]=OFF),DO[119:OFF:260 Purge Complete]=ON ;
  62:  !-- ADJUST COLLISION DETECT ;
  63:  COL GUARD ADJUST R[39] ;
  64:  DO[115:OFF:Part 1 Unloaded 260]=ON ;
  65:  WAIT (DI[115:OFF:260GX Part 1 Req for ]=OFF)    ;
  66:  DO[115:OFF:Part 1 Unloaded 260]=OFF ;
  67:  IF (DI[43:OFF:PURGE REQUEST]=OFF AND DO[419:ON :G1 BCR1 OK]=ON) THEN ;
  68:  WAIT (DI[116:OFF:260GX Part 1 Req for ]=ON)    ;
  69:  ENDIF ;
  70:  ENDIF ;
  77:  !******************************** ;
  78:   ;
  79:  !-- PLACE TO 260 ;
  80:  !----------------------------- ;
  81:  IF (DI[116:OFF:260GX Part 1 Req for ]=ON AND DO[419:ON :G1 BCR1 OK]=ON) THEN ;
  82:  WAIT (DI[118:OFF:260GX Part 1 Released]=ON AND DI[117:OFF:260GX Part 1 Gripped]=OFF)    ;
  83:  !-- RESET SAFE OUTSIDE 260 ;
  84:  DO[114:ON :Robot Clear 260]=OFF ;
  85:  !-- CHANGE TOOL AND USER FRAME ;
  86:  UTOOL_NUM=1 ;
  87:  UFRAME_NUM=1 ;
  88:   ;
  89:J PR[30] 100% CNT50    ;
  90:   ;
  91:  CALL SET_OFFS(25,0,(-600),0,0,0,0,0) ;
  92:L PR[R[101]] 2000mm/sec CNT50 Tool_Offset,PR[R[93]] Offset,PR[25:Cartesian Offset]    ;
  93:   ;
  94:L PR[R[101]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]]    ;
  95:  !-- ADJUST COLLISION DETECT ;
  96:  COL GUARD ADJUST R[39] ;
  97:L PR[R[101]] 250mm/sec FINE    ;
  98:  !-- OPEN GRIPPER ;
  99:  CALL GRIP_1_OPEN    ;
 100:  WAIT    .50(sec) ;
 101:  DO[419:ON :G1 BCR1 OK]=OFF ;
 102:   ;
 103:  !-- RETREAT FROM 260GX ;
 104:L PR[R[101]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]]    ;
 105:   ;
 106:  CALL SET_OFFS(25,0,(-600),0,0,0,0,0) ;
 107:L PR[R[101]] 2000mm/sec CNT10 Tool_Offset,PR[R[93]] Offset,PR[25:Cartesian Offset]    ;
 108:   ;
 109:L PR[30] 2000mm/sec FINE    ;
 112:   ;
 113:  DO[114:ON :Robot Clear 260]=ON ;
 114:  !-- ADJUST COLLISION DETECT ;
 115:  COL GUARD ADJUST R[39] ;
 116:  DO[116:OFF:Part 1 Loaded 260]=ON ;
 117:  WAIT (DI[116:OFF:260GX Part 1 Req for ]=OFF)    ;
 118:  DO[116:OFF:Part 1 Loaded 260]=OFF ;
 119:  ENDIF ;
 120:   ;
 121:  LBL[1000] ;
 122:  !-- CHANGE TOOL AND USER FRAME ;
 123:  UTOOL_NUM=9 ;
 124:  UFRAME_NUM=1 ;
 125:  !-- MOVE TO SAFE ;
 126:J PR[4:Safe 260 Side] 100% CNT50    ;
 127:  END ;
/POS
/END
